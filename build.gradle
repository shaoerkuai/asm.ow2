description = 'Website of the ASM project'

repositories { mavenCentral() }

apply plugin: 'java'

dependencies { 
  implementation 'org.apache.commons:commons-csv:1.5'
  implementation 'org.freemarker:freemarker:2.3.28' 
}

task javadoc(type: Javadoc, overwrite: true) {
  ['asm', 'asm-analysis', 'asm-commons', 'asm-tree', 'asm-util'].each { pkg ->
    source(fileTree("external/${pkg}/src/main/java").include('**/*.java'))
  }
  title = 'ASM 9.0 beta'
  destinationDir = file('public/javadoc')
}

task runBenchmark(type: GradleBuild) {
  dir 'external'
  startParameter.setTaskNames(['benchmarks:jmh'])
}

// Manual task to run the benchmarks and to regenerate the performance.html page
// from the results (takes ~55m).
task benchmark(type: JavaExec, dependsOn: 'runBenchmark') {
  classpath sourceSets.main.runtimeClasspath
  main 'ResultProcessor'
  args 'external/benchmarks/build/reports/jmh/results.csv'
  args 'src/main/html/performance.html'
}

task publish(type: Copy, dependsOn: 'javadoc') {
  inputs.files('src/main/html/header.inc')
  def header = file('src/main/html/header.inc').text
  def license = file('src/main/objects/LICENSE.txt').text
  from('src/main/html') {
    exclude '*.inc'
    filter({line -> line.replace('<!--header-->', header)})
    filter({line -> line.replace('<!--license-->', "<pre>${license}</pre>")})
  }
  from 'src/main/images'
  from 'src/main/objects'
  into 'public'
}

clean.doFirst {
  delete 'public'
}
